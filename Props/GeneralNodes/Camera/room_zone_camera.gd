class_name RoomZoneCamera extends Camera2D



var overlapping_zones: Array[CameraZoneArea] = []
@export var active_zone: CameraZoneArea
@export var player: Player

@export var follow_player: bool = false




func _process(_delta: float) -> void:
	if not player:
		print("no found player")
		return


	if overlapping_zones.is_empty() or overlapping_zones.size() == 1 and active_zone == overlapping_zones[0]:
		return

	var new_zone = get_closest_zone()
	if new_zone != active_zone:
		active_zone = new_zone
		apply_zone_settings()




func _physics_process(_delta: float) -> void:
	if follow_player and player:
		global_position = player.global_position


func get_closest_zone() -> CameraZoneArea:
	var  closest_zone: CameraZoneArea = null
	var closest_dist: float = INF
	var player_pos = player.global_position
	
	
	for zone in overlapping_zones:
		var zone_shape: CollisionShape2D = zone.collision_shape
		var col_margin: float = 0.1
		var zone_shape_pos: Vector2 = zone_shape.global_position
		var zone_shape_extents: Vector2 = zone_shape.shape.extents
		var shape_sides: Array[Vector2] = [
			Vector2(zone_shape_pos.x - zone_shape_extents.x + col_margin, player_pos.y),
			Vector2(zone_shape_pos.x + zone_shape_extents.x - col_margin, player_pos.y),
			Vector2(player_pos.x, zone_shape_pos.y - zone_shape_extents.y + col_margin),
			Vector2(player_pos.x, zone_shape_pos.y + zone_shape_extents.y - col_margin )
	 ]
	
		var closest_dist_shapeside: float = INF
		for col_side in shape_sides:
			var col_dist: float = player_pos.distance_to(col_side)
			if col_dist < closest_dist_shapeside:
				closest_dist_shapeside = col_dist
	
		if closest_dist_shapeside < closest_dist:
			closest_dist = closest_dist_shapeside
			closest_zone = zone
	
	return closest_zone




func apply_zone_settings() -> void:
	if active_zone == null:
		return
	
	zoom = active_zone.zoom
	
	follow_player = active_zone.follow_player
	if not active_zone.follow_player:
		global_position = active_zone.fixed_position
		
	if active_zone.limit_camera:
		limit_enabled = true
		limit_left = active_zone.limit_left
		limit_top = active_zone.limit_top
		limit_right = active_zone.limit_right
		limit_bottom = active_zone.limit_bottom
	else:
		limit_enabled = false
